#Область СлужебныеПроцедурыИФункции

 Процедура ДобавлениеКонтактногоЛица(Форма) Экспорт
	 
	 ИмяФормы = Форма.ИмяФормы;
	 
	 Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		 ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		 ДобавитьСкидкуВГруппаШапкаЛево(Форма);
		 
	 ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		 ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);	 
	 ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		 ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);	 
	 ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		 ВставитьПолеКонтактноеЛицоПередСуммаДокумента(Форма);	 
	 ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		 ВставитьПолеКонтактноеЛицоПередСуммаДокумента(Форма);
	 КонецЕсли;	 	 
	 
 КонецПроцедуры
 
 
 Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	 
	 Если Форма.Элементы.Найти("КонтактноеЛицо") = Неопределено Тогда
		 ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
		 ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		 ПолеВвода.ПутьКДанным = "Объект.ДЗПотанин_КонтактноеЛицо";
	 КонецЕсли;
	 
 КонецПроцедуры
 
 Процедура ВставитьПолеКонтактноеЛицоПередСуммаДокумента(Форма)
	 
	 Если Форма.Элементы.Найти("КонтактноеЛицо") = Неопределено Тогда
		 ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.СуммаДокумента);
		 ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		 ПолеВвода.ПутьКДанным = "Объект.ДЗПотанин_КонтактноеЛицо";
	 КонецЕсли;
	 
 КонецПроцедуры
 
 Процедура ДобавитьСкидкуВГруппаШапкаЛево(Форма)
	 
	 Если Форма.Элементы.Найти("ГруппаСкидка") = Неопределено Тогда
		 ГруппаСкидка = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"),Форма.Элементы.ГруппаШапкаЛево);
		 ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		 ГруппаСкидка.ОтображатьЗаголовок = Ложь;
		 ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	 КонецЕсли;
	 
	 Если Форма.Элементы.Найти("СогласованнаяСкидка") = Неопределено Тогда
		 ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
		 ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		 ПолеВвода.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении");
		 ПолеВвода.ПутьКДанным = "Объект.ДЗПотанин_СогласованнаяСкидка";
	 КонецЕсли;
	 
	 Если Форма.Элементы.Найти("КнопкаРассчитатьСкидку") = Неопределено Тогда
		 КомандаРассчитатьСкидку = Форма.Команды.Добавить("РассчитатьСкидку");
		 КомандаРассчитатьСкидку.Заголовок = "Рассчитать скидку";
		 КомандаРассчитатьСкидку.Действие = "КомандаРассчитатьСкидку";	 
		 
		 КнопкаФормыРассчитатьСкидку = Форма.Элементы.Добавить("КнопкаРассчитатьСкидку", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
		 КнопкаФормыРассчитатьСкидку.ИмяКоманды = "РассчитатьСкидку";
		 КнопкаФормыРассчитатьСкидку.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		 КнопкаФормыРассчитатьСкидку.Отображение = ОтображениеКнопки.КартинкаИТекст;
		 КнопкаФормыРассчитатьСкидку.Картинка = БиблиотекаКартинок.Перечитать;
	 КонецЕсли; 
	 
 КонецПроцедуры  
 

#КонецОбласти